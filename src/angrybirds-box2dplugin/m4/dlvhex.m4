# dlvhex.m4: Locate dlvhex path, headers, and libraries for autoconf-based projects.
# Copyright (C) 2012  Peter Schueller <ps@kr.tuwien.ac.at>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Additional permission under section 7 of the GNU General Public
# License, version 3 ("GPLv3"):
#
# If you convey this file as part of a work that contains a
# configuration script generated by Autoconf, you may do so under
# terms of your choice.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Original sources can be found in SVN of the SourceForge project of dlvhex
# http://sourceforge.net/projects/dlvhex/

# DLVHEX_REQUIRE([VERSION])
# -----------------------------------------------
# Look for dlvhex.  Version must be a literal of the form "X.Y.Z".
# Provides and uses configure argument --with-dlvhex2=<directory>
# uses pkg-config
# defines DLVHEX_USERPLUGINDIR (substituted in ".in" files)
# defines DLVHEX_SYSPLUGINDIR (substituted in ".in" files)
# defines DLVHEX_BINDIR (substituted in ".in" files)
# If dlvhex cannot be found, aborts with an error message.
AC_DEFUN([DLVHEX_REQUIRE],dnl
[AC_ARG_WITH([dlvhex2],[dnl
AS_HELP_STRING([--with-dlvhex2=DIR],dnl
[prefix of where you installed dlvhex2 >= $1])])
if test "x$with_dlvhex2" != "x"; then
  export PKG_CONFIG_PATH="${with_dlvhex2}lib/pkgconfig/"
AC_SUBST([DISTCHECK_CONFIGURE_FLAGS],dnl
["$DISTCHECK_CONFIGURE_FLAGS --with-dlvhex2=${with_dlvhex2}"])dnl
fi
PKG_CHECK_MODULES([DLVHEX],[dlvhex2 >= $1])
_PKG_CONFIG([DLVHEX_USERPLUGINDIR],[variable=userplugindir],[dlvhex2 >= $1])
_PKG_CONFIG([DLVHEX_SYSPLUGINDIR],[variable=sysplugindir],[dlvhex2 >= $1])
_PKG_CONFIG([DLVHEX_BINDIR],[variable=bindir],[dlvhex2 >= $1])
DLVHEX_USERPLUGINDIR=$pkg_cv_DLVHEX_USERPLUGINDIR
DLVHEX_SYSPLUGINDIR=$pkg_cv_DLVHEX_SYSPLUGINDIR
DLVHEX_BINDIR=$pkg_cv_DLVHEX_BINDIR
AC_SUBST(DLVHEX_USERPLUGINDIR)
AC_SUBST(DLVHEX_SYSPLUGINDIR)
AC_SUBST(DLVHEX_BINDIR)
SAVE_CPPFLAGS="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $DLVHEX_CFLAGS"
AC_CHECK_HEADER([dlvhex2/OnlineModelBuilder.h],,dnl
[AC_MSG_ERROR([Cannot find dlvhex2 headers!])])
CPPFLAGS="$SAVE_CPPFLAGS"
])

# DLVHEX_DEFINE_VERSION([PREFIX],[VERSION])
# -----------------------------------------------
# VERSION must be a literal of the form "X.Y.Z".
# defines <PREFIX>_VERSION, <PREFIX>_VERSION_MAJOR,
# <PREFIX>_VERSION_MINOR, <PREFIX>_VERSION_MICRO
# (these are substituted in .in files and in config.h)
AC_DEFUN([DLVHEX_DEFINE_VERSION],dnl
[pushdef([NAME],[$1])
NAME[_VERSION]="$2"
NAME[_VERSION_MAJOR]=`echo "$2" |sed "s,^\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\),\1,"`
NAME[_VERSION_MINOR]=`echo "$2" |sed "s,^\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\),\2,"`
NAME[_VERSION_MICRO]=`echo "$2" |sed "s,^\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\),\3,"`
AC_SUBST(NAME[_VERSION])
AC_SUBST(NAME[_VERSION_MAJOR])
AC_SUBST(NAME[_VERSION_MINOR])
AC_SUBST(NAME[_VERSION_MICRO])
AC_DEFINE_UNQUOTED(NAME[_VERSION],[$]NAME[_VERSION],[Version number of ]NAME)
AC_DEFINE_UNQUOTED(NAME[_VERSION_MAJOR],[$]NAME[_VERSION_MAJOR],[Major version number of ]NAME)
AC_DEFINE_UNQUOTED(NAME[_VERSION_MINOR],[$]NAME[_VERSION_MINOR],[Minor version number of ]NAME)
AC_DEFINE_UNQUOTED(NAME[_VERSION_MICRO],[$]NAME[_VERSION_MICRO],[Micro version number of ]NAME)
popdef([NAME])
])
